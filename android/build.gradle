buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['RenderLynx_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["RenderLynx_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.renderlynx"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  // lynx dependencies
  implementation "org.lynxsdk.lynx:lynx:3.2.0-rc.0"
  implementation "org.lynxsdk.lynx:lynx-jssdk:3.2.0-rc.0"
  implementation "org.lynxsdk.lynx:lynx-trace:3.2.0-rc.0"
  implementation "org.lynxsdk.lynx:primjs:2.11.1-rc.0"

  // integrating image-service
  implementation "org.lynxsdk.lynx:lynx-service-image:3.2.0-rc.0"

  // // image-service dependencies, if not added, images cannot be loaded; if the host APP needs to use other image libraries, you can customize the image-service and remove this dependency
  // implementation "com.facebook.fresco:fresco:2.3.0"
  // implementation "com.facebook.fresco:animated-gif:2.3.0"
  // implementation "com.facebook.fresco:animated-webp:2.3.0"
  // implementation "com.facebook.fresco:webpsupport:2.3.0"
  // implementation "com.facebook.fresco:animated-base:2.3.0"

  // implementation "com.squareup.okhttp3:okhttp:4.9.0"

  // integrating log-service
  implementation "org.lynxsdk.lynx:lynx-service-log:3.2.0-rc.0"

  // integrating http-service
  implementation "org.lynxsdk.lynx:lynx-service-http:3.2.0-rc.0"
}

react {
  jsRootDir = file("../src/")
  libraryName = "RenderLynxView"
  codegenJavaPackageName = "com.renderlynx"
}
